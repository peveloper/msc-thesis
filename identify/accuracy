reset
set terminal pngcairo size 1400, 1000 dashed enhanced font "Helvetica, 18"
plot_dir = "plots/"
filename=ARG1
print filename
title=ARG2
id=(ARG2[1:strstrt(ARG2, "_")-1])+0
tp=(ARG2[strstrt(ARG2, "_")+1:]+0)
if (tp >= 1000) tp=sprintf("%d Mbps", tp/1000); else tp=sprintf("%d Kbps", tp)
set title sprintf("Matches, collisions and mismatches for ID %d at %s at different window sizes, MODE[%s], Key Δ = %s", id, tp, ARG3, ARG4)
set output sprintf("%s%s.png", plot_dir, filename)
set style histogram cluster gap 2
set style data histograms
set style fill transparent solid 0.3 border
set xlabel "Window Sizes (Key Sizes)"
set ylabel sprintf("Number of windows with ρ > %.2f", ARG5+0)
set key outside
set grid
set yrange [0:*]
set ytics nomirror
plot filename using 2 ti col lt rgb "green", '' u 3 ti col lt rgb "blue", '' u 4:xticlabels(1) ti col lt rgb "red"
stats_filename = "stats/stats_".filename
set print stats_filename
print "Matches"
stats filename using (column(2)) name "matches"
print "--------------------------------------------------------------------------------"
print "Collisions"
stats filename using (column(3)) name "collisions"
print "--------------------------------------------------------------------------------"
print "Mismatches"
stats filename using (column(4)) name "mismatches"



