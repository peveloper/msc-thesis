reset
set terminal pngcairo size 1200, 800 dashed enhanced
plot_dir = "plots/"
filename=ARG1
title=ARG2
id=(ARG2[1:strstrt(ARG2, "_")-1])+0
tp=(ARG2[strstrt(ARG2, "_")+1:]+0) / 1000
set title sprintf("Frequency of matches, collisions and mismatches for ID %d at %.2fMbps at different window sizes, MODE[%s], Key Δ = %s", id, tp, ARG3, ARG4)
set output sprintf("%s%s.png", plot_dir, filename)
set style histogram cluster gap 2
set style data histograms
set style fill transparent solid 0.3 border
set xtics font ", 10"
set ytics font ", 10"
set xlabel "Window Sizes (Key Sizes)"
set ylabel sprintf("Number of windows with ρ > %.2f", ARG5+0)
set key outside
set grid
set yrange [0:*]
set ytics nomirror
plot filename using 2 ti col lt rgb "green", '' u 3 ti col lt rgb "blue", '' u 4:xticlabels(1) ti col lt rgb "red"
