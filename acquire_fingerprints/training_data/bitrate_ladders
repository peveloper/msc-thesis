set terminal pngcairo size 1200, 800 dashed enhanced font "Helvetica,18"
plot_dir=ARG2
filename=ARG1
set xlabel "Enforced Throughputs (bits/s)" offset -0.1,0,0
set ylabel "AVG Bitrate (bits/s)"
set decimal locale
set grid
set key bottom right
set format "%.1s%c"
set output sprintf("%s%s_ladder.png", plot_dir, filename)
stats_filename="stats/".filename
file=filename[1:strstrt(filename, ".")-1]
set print stats_filename
stats filename using (column(2) - column(4)) nooutput name "diff"
stats filename using (column(2)) nooutput name "real"
stats filename using (column(4)) nooutput name "obs"
rmse=sqrt(diff_sumsq / diff_records) / obs_mean
record=sprintf("%s & %.2f & %.2f & %.2f & %.2f & %.2f & %.2f & %.2f", file, real_mean / 1000000, real_stddev / 1000000, real_median / 1000000, obs_mean /1000000, obs_stddev / 1000000, obs_median/ 1000000 , rmse)
print record
set title sprintf("Real vs. Reconstructed Bitrate Ladders ID: %s, Î¼-normalized RMSE %.2f", file, rmse) noenhanced
plot filename u 1:4 with lp t "REAL", '' u 1:2 with lp t "RECONSTRUCTED"
